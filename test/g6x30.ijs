NB. 1:@:(9!:19)2^_44[(echo^:ECHOFILENAME) './g6x30.ijs'
NB. 6!:30 ----------------------------------------------------------------

tsl=: 6!:30
tsu=: 6!:31
tsp=: 6!:32

t=: tsl ''
(,6) = $t
(-: <.) 5{.t
*./ 0 <: t
1990 <: 0{t
*./ (1&<: *. <:&12 31) 1 2{t
24 60 60 *./ . > 3 4 5{t

t=: tsu ''
(,6) = $t
(-: <.) 5{.t
*./ 0 <: t
1990 <: 0{t
*./ (1&<: *. <:&12 31) 1 2{t
24 60 60 *./ . > 3 4 5{t

t=: tsl x=: '%Y-%m-%d %H:%M:%S'
($t) -: ,19
t -:&(3!:0) x
(2{.t) -: '20'

t=: tsu x=: '%Y-%m-%d %H:%M:%S'
($t) -: ,19
t -:&(3!:0) x
(2{.t) -: '20'

'2019-01-02 03:04:05' -: 2019 1 2 3 4 5.6 tsl '%Y-%m-%d %H:%M:%S'
'2019-01-02 03:04:05' -: {. (5 6$2019 1 2 3 4 5.6) tsl '%Y-%m-%d %H:%M:%S'
(2 10$'2019-01-022019-02-03') -: (2 3$2019 1 2 2019 2 3) tsl '%Y-%m-%d'
(,0) -: $  2019 1 2 3 4 5.6 tsl '%Y-%m-%d %H:%M:%s'
(2 15$'January,02 2019May,03 2019    ') -: (2 3$2019 1 2 2019 5 3) tsl '%B,%d %Y'

'2019-01-02 03:04:05' -: 2019 1 2 3 4 5.6 tsu '%Y-%m-%d %H:%M:%S'
'2019-01-02 03:04:05' -: {. (5 6$2019 1 2 3 4 5.6) tsu '%Y-%m-%d %H:%M:%S'
(2 10$'2019-01-022019-02-03') -: (2 3$2019 1 2 2019 2 3) tsu '%Y-%m-%d'
(,0) -: $  2019 1 2 3 4 5.6 tsu '%Y-%m-%d %H:%M:%s'
(2 15$'January,02 2019May,03 2019    ') -: (2 3$2019 1 2 2019 5 3) tsu '%B,%d %Y'

2019 12 23 12 34 56 -: '%Y-%m-%d %H:%M:%S' tsp '2019-12-23 12:34:56'
(2 6$2019 12 23 12 34 56 2001 1 2 1 2 3) -: '%Y-%m-%d %H:%M:%S' tsp 2 19$'2019-12-23 12:34:562001-01-02 01:02:03'
(2 6$2019 12 23 12 34 56.123 2001 1 2 1 2 3.678) -: '%Y-%m-%d %H:%M:%S.%f' (6!:32) 2 23$'2019-12-23 12:34:56.1232001-01-02 01:02:03.678'

'domain error' -: tsl etx 3 4 5
'domain error' -: tsl etx <'%Y-%m-%d %H:%M:%S'
'domain error' -: tsu etx 3 4 5
'domain error' -: tsu etx <'%Y-%m-%d %H:%M:%S'

'rank error'   -: tsl etx ,: '%Y-%m-%d %H:%M:%S'
'rank error'   -: tsu etx ,: '%Y-%m-%d %H:%M:%S'

'domain error' -: tsp etx '2019-12-23 12:34:56'
'domain error' -: '%Y-%m-%d %H:%M:%S' tsp etx 3 4 5

1 [ 6!:3 (55&< * 60&-) 5{tsl '' NB. delay til a new minute if close to it
(}.;'-'&,@}.@":&.>10000 100 100+3{.tsl '') -: tsl '%Y-%m-%d'
(tsl -: tsl@u:) '%Y-%m-%d'
(tsl -: tsl@u:) '%m/%d/%y %H:%M'
(tsl -: tsl@(10&u:)) '%Y-%m-%d'
(tsl -: tsl@(10&u:)) '%m/%d/%y %H:%M'
(":{.tsl '') -: tsl    '%Y'
(_2{.":{.tsl '') -: tsl '%y'

1 [ 6!:3 (55&< * 60&-) 5{tsu '' NB. delay til a new minute if close to it
(}.;'-'&,@}.@":&.>10000 100 100+3{.tsu '') -: tsu '%Y-%m-%d'
(tsu -: tsu@u:) '%Y-%m-%d'
(tsu -: tsu@u:) '%m/%d/%y %H:%M'
(tsu -: tsu@(10&u:)) '%Y-%m-%d'
(tsu -: tsu@(10&u:)) '%m/%d/%y %H:%M'
(":{.tsu '') -: tsu    '%Y'
(_2{.":{.tsu '') -: tsu '%y'

4!:55 ;:'t tsl tsu tsp x'
